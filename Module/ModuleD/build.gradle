if (rootProject.ext.module.isModule) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion

    defaultConfig {
        if (rootProject.ext.module.isModule) {
            applicationId "com.enzo.module_d"
        }
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName

        //是否是组件开发模式
        buildConfigField("Boolean", "IS_MODULE", String.valueOf(rootProject.ext.module.isModule))
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            if (rootProject.ext.module.isModule) {
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                //集成开发模式下排除debug文件夹中的所有Java文件
                java {
                    exclude 'debug/**'
                }
            }
        }
    }

    resourcePrefix 'md_'
}

dependencies {
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':BaseLib:CommonLib')
    implementation project(':BaseLib:FLKit')
    //arouter
    api rootProject.ext.dependencies.arouter
    annotationProcessor rootProject.ext.dependencies.arouter_annotation
    //auto-service
    annotationProcessor rootProject.ext.dependencies.auto_service
    compileOnly rootProject.ext.dependencies.auto_service
    //room
    implementation "android.arch.persistence.room:runtime:1.1.1"
    annotationProcessor "android.arch.persistence.room:compiler:1.1.1"
}
